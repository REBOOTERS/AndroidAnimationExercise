apply plugin: 'com.android.application'

// Android Gradle Plugin source @ https://android.googlesource.com/platform/tools/base/+/gradle_3.1.2

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply plugin: "com.dorongold.task-tree"
//apply plugin: 'android-aspectjx'
apply plugin: 'jacoco-android'


def ext = rootProject.ext

/**
 * @link https://mp.weixin.qq.com/s/WDap7bmM6gP7koIjEIwTtQ Gradle更小、更快构建APP的奇淫技巧
 */

android {
    signingConfigs {
        debug {
            keyAlias 'animation'
            keyPassword '123456'
            storeFile file("$rootDir/animationkey")
            storePassword '123456'
        }
    }
    compileSdkVersion ext.compileSdkVersion
    defaultConfig {
        applicationId "home.smart.fly.animations"
        minSdkVersion ext.minSdkVersion
        targetSdkVersion ext.targetSdkVersion
        versionCode 5
        versionName "5.0"
        flavorDimensions "versionCode"
        //blur
        renderscriptTargetApi 19
        renderscriptSupportModeEnabled true

        //
        vectorDrawables.useSupportLibrary true

        //65536
        multiDexEnabled true

//        kapt {
//            arguments {
//                arg("AROUTER_MODULE_NAME", project.getName())
//            }
//        }
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }

        aaptOptions {
            // build 过程不会对 png 进行优化，节省时间
            cruncherEnabled = false
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testOptions {
            unitTests {
                includeAndroidResources = true
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            splits.abi.enable = false
            splits.density.enable = false
        }
    }

    // just for study ,without real meaning
    flavorDimensions "default"
    productFlavors {
        free {
            versionNameSuffix "_free"
            resConfigs('ch', 'xhdpi')
            minSdkVersion 21
            aaptOptions.cruncherEnabled = false
            ext.alwaysUpdateBuildId = false
        }

        charge {
            versionNameSuffix "_charge"
            resConfigs('en', 'xhdpi')
            minSdkVersion 21
            aaptOptions.cruncherEnabled = false
            ext.alwaysUpdateBuildId = false
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    dexOptions {
        preDexLibraries true
        maxProcessCount 8
    }

    lintOptions {
        abortOnError false
    }

}


configurations.all {
//    resolutionStrategy.force 'androidx.core:core:1.0.2'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation "androidx.appcompat:appcompat:$ext.support_versioin_appcompat"
    implementation "com.google.android.material:material:$ext.support_versioin_material"
    implementation 'com.orhanobut:logger:2.2.0'
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'de.hdodenhof:circleimageview:3.0.1'
    implementation "androidx.legacy:legacy-support-v4:$ext.support_versioin"
    implementation "androidx.cardview:cardview:$ext.support_versioin"
    implementation "com.github.bumptech.glide:glide:$ext.glide"
    implementation 'com.squareup.okhttp3:okhttp:4.2.2'
    implementation 'com.squareup.okio:okio:2.4.1'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation "androidx.vectordrawable:vectordrawable:1.1.0"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'cn.bingoogolapple:bga-refreshlayout:1.1.8@aar'
    implementation 'cn.bingoogolapple:bga-banner:2.2.4@aar'
    implementation 'cn.bingoogolapple:bga-adapter:1.2.0@aar'
    implementation 'com.jakewharton:butterknife:10.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    kapt 'com.jakewharton:butterknife-compiler:10.2.0'
    implementation 'com.facebook.stetho:stetho:1.5.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.dynamicanimation:dynamicanimation:$support_versioin"
    implementation 'com.airbnb.android:lottie:3.1.0'
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.10.0'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.0-beta-3'
    implementation 'net.sourceforge.streamsupport:streamsupport:1.7.1'
    implementation "com.alibaba:arouter-api:$ext.arouter_api"
    kapt "com.alibaba:arouter-compiler:$ext.arouter_compiler"
    kapt 'com.zhihu.android:sugaradapter-processor:1.8.6'
    implementation "androidx.palette:palette:$support_versioin"
    implementation 'com.tencent:mmkv:1.0.18'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation 'com.jakewharton.hugo:hugo-runtime:1.2.1'

    // https://github.com/didi/DoraemonKit

    debugImplementation 'com.didichuxing.doraemonkit:doraemonkit:1.1.7'
    releaseImplementation 'com.didichuxing.doraemonkit:doraemonkit-no-op:1.1.7'


    if (!imitate_isApp.toBoolean()) {
        implementation project(':imitate')
    }
    implementation project(path: ':dataview')
    implementation project(path: ':skeleton')

}

ext.allDependencies.each {
    dependencies.add('implementation', it)
}


repositories {
    mavenCentral()
}


// 自定义插件的一些测试，如果无法运行，可以暂时注释掉
apply from: file("../gradle/custom_plugin.gradle")
apply from: file("../gradle/test.gradle")
